#!/usr/bin/perl

use Text::BibTeX;


##### The URLs below are relative to where the HTML pages will be
##### finally setup (not used during execution of bibTOhtml). $destdir
##### tells bibTOhtml where to put the .html files relative to the current path
$style = "/style.css"; # URL of a style sheet to be included
$picdir = "/i";        # URL of where the images and icons are
$verbose = 0;          # show file open/close info during parsing
$bibmaster = "src/ilab.bib";   # URL of master bibtexfile
$destdir = '/home/httpd/html/publications'; # PATH where to write HTML

##### You Lab's Information & your Copyright:
$title = "iLab Publications - University of Southern California";
$copy = "Copyright &copy; 2002 by the University of Southern California, ".
    "iLab and Prof. Laurent Itti.";

##### Colors:
$bgcol = "#CCF0FF"; # color for page background
$ticol = 'navy';    # color for titles
$jocol = 'red';     # color for journal names
$dacol = '#EE6600'; # color for year
$xxcol = 'blue';    # for the words: Abstract, Note, Keywords

##### Images and Icons:
$bannerPic = $picdir."/ilab5.gif"; # a banner at top of page (or comment out)
$titlePic = $picdir."/publi1.gif"; # a title just below banner (or comment out)
$ICONps = $picdir."/ICONps.gif";
$ICONpdf = $picdir."/ICONpdf.gif";
$ICONhtml = $picdir."/ICONhtml.gif";
$ICONabs = $picdir."/ICONabs.gif";
$ICONblank = $picdir."/ICONblank.gif";
$ICONbib = $picdir."/ICONbib.gif";

##### The real BibTeX entry types plus a couple of fake ones:
%types = ( 'article' => 'Journal Articles',
	   'incollection' => 'Book Chapters',
	   'inproceedings' => 'Proceedings from International Conferences',
	   'book' => 'Books',
	   'phdthesis' => 'Ph.D. Theses',
	   'mastersthesis' => 'Master Theses',
	   'techreport' => 'Technical Reports',
	   'booklet' => 'Booklets',
	   'manual' => 'Technical Documentations',
	   'proceedings' => 'Edited Conference Proceedings',
	   'unpublished' => 'Unpublished Documents',
	   'patent' => 'Patents and Copyrights',

	   'submitted' => 'Publications Submitted to Review',
	   'in-press' => 'Publications in Press',
	   'all' => 'All Publications',
	   );
@origtypes = keys(%types); $toda = `date`;
$types{'index'} = 'Welcome to the iLab Publication Server!';

##### The thematic categories: They should be stored in a "theme" BibTeX tag,
##### separated by | if there are several:
$types{'bu'} = 'Model of Bottom-Up Saliency-Based Visual Attention';
$types{'td'} = 'Model of Top-Down Attentional Modulation';
$types{'psy'} = 'Human Psychophysics';
$types{'mod'} = 'Computational Modeling';
$types{'mip'} = 'Medical Image Processing';
$types{'fmri'} = 'Functional Neuroimaging';
$types{'med'} = 'Medical Research';
$types{'cv'} = 'Computer Vision';
$types{'rev'} = 'Review Articles and Chapters';
$types{'bb'} = 'Beobots';
$types{'sc'} = 'Scene Understanding';

##############################################################################
##### This starts gettign messy and should not be modified:
##############################################################################
%info = ( 'phdthesis' => 'Ph.D. Thesis',
	  'mastersthesis' => 'Master Thesis',
	  'techreport' => 'Technical Report',
	  'manual' => 'Technical Documentation',
	  'unpublished' => 'Unpublished',
	  'patent' => 'Patent/Copyright'
      );
$HTMLstart = "<HTML><HEAD><LINK rel=\"stylesheet\" href=\"$style\">".
    "<TITLE>$title</TITLE><META HTTP-EQUIV=\"Pragma\" CONTENT=\"no-cache\">".
	"</HEAD><BODY BGCOLOR=\"$bgcol\">\n";
if ($bannerPic) {
    $HTMLstart .= "<P><CENTER><IMG SRC=\"$bannerPic\"></IMG></CENTER></P>\n";
}
if ($titlePic) {
    $HTMLstart .= "<P><CENTER><IMG SRC=\"$titlePic\"></IMG></CENTER></P>\n";
}

$HTMLend = "<P> &nbsp; </P><CENTER><P class=\"sm\">$copy<BR>".
    "This page generated by <A HREF=\"http://iLab.usc.edu/bibTOhtml/\">".
    "bibTOhtml</A> on $toda</P></CENTER></BODY></HTML>\n";

$HTMLicons = "<HR><CENTER><P class=\"desc\"><IMG SRC=\"$ICONhtml\" ".
    "BORDER=0></IMG> = HTML Version, &nbsp; &nbsp; <IMG ".
    "SRC=\"$ICONpdf\" BORDER=0></IMG> = PDF Version, &nbsp; &nbsp; <IMG ".
    "SRC=\"$ICONbib\" BORDER=0></IMG> = BibTeX entry, &nbsp; &nbsp; ".
    "<IMG SRC=\"$ICONabs\" BORDER=0></IMG> = Online ".
    "Abstract</CENTER></P>".
    #"<CENTER><P class=\"BIBkw\">NOTE: The pdf ".
    #"files are compressed with gzip, which will confuse older ".
    #"browsers. If they appear corrupted, <A HREF=\"doc/problem.html\">".
    #"see here.</A></P></CENTER>".
    "<HR>";

#### let's also build a latex list of publications for my CV:
$ltxstart = "\\vspace{0.1cm}\n\\begin{center}{\\Large\\bf Scientific ".
    "Publications}\\end{center}\n\\vspace{0.1cm}\n\n";
$ltx1 = "\\noindent {\\large\\bf Refereed Journal Articles and Book ".
    "Chapters}\n\n\\begin{enumerate} \\parsep=3pt \\itemsep=0pt\n\n";
$ltx2 = "\\noindent {\\large\\bf Refereed Long Proceedings from Internatio".
    "nal Conferences}\n\n\\begin{enumerate} \\parsep=3pt \\itemsep=0pt\n\n";
$ltx3 = "\\noindent {\\large\\bf Abstracts of Presentations at International ".
    "Conferences}\n\n\\begin{enumerate} \\parsep=3pt \\itemsep=0pt\n\n";
$ltx4 = "\\noindent {\\large\\bf Other Publications}\n\n".
    "\\begin{enumerate} \\parsep=3pt \\itemsep=0pt\n\n";
$ltxend = "\\end{enumerate}\n\n";
$ltxnote = "\\noindent{\\small\\fontfamily{cmss}\\selectfont [Publication ".
    "list automatically compiled: ".`date`."- Check http://iLab.usc.edu ".
    "for up-to-date information].}\n\n";

##############################################################################
$bibname = shift || die "USAGE: $0 <bibfile.bib>\n";
$nps = 0; $nhtm = 0; $npdf = 0; $nabs = 0; $nb = 0;

##### start parsing the bibtex file:
$bibfile = new Text::BibTeX::File $bibname;
while ($entry = new Text::BibTeX::Entry $bibfile)
{
    my ($lti, $ljo, $lvo, $lbt, $led, $ldat, $lin);
    next unless $entry->parse_ok;
    next unless $entry->metatype eq BTE_REGULAR;

    ##### parse entry:
    $type = $entry->type; $ye = $entry->get('year');
    $ye = lc($ye); if ($ye =~ /^\d+$/ && $ye < 100) { $ye += 1900; }
    $key = $entry->key; $au = join(', ', $entry->split('author'));
    $ti = $entry->get('title');     $jo = $entry->get('journal');
    if (substr($ti, -1) eq '.') { chop $ti; }
    $vo = $entry->get('volume');    $nu = $entry->get('number');
    $mo = $entry->get('month');     $pp = $entry->get('pages');
    $se = $entry->get('series');    $pu = $entry->get('publisher');
    $bt = $entry->get('booktitle');
    $ed = join(', ', $entry->split('editor'));
    $ad = $entry->get('address');   $or = $entry->get('organization');
    $in = $info{$type};

    #print STDERR "$key, $jo$bt, $ye [$type]\n";
    print STDERR "$key [$type]\n";
    ##### format and colorize the various fields:
    if ($au) { $au = "$au, "; }
    if ($ti) { $lti = "``$ti,'' ";  # for latex output
	       $ti = "<FONT COLOR=\"$ticol\">$ti,</FONT> "; }  # for html out
    if ($jo) { $ljo = "{\\it $jo,} ";
	       $jo = "<FONT COLOR=\"$jocol\"><I>$jo,</I></FONT> ";}
    if ($vo) { $lvo = "Vol. {\\bf $vo}, ";
	       $vo = "Vol. <B>$vo</B>, "; }
    if ($nu) { $nu = "No. $nu, "; }
    if ($ye) { $ldat = "$mo $ye.";
	       $dat = "<FONT COLOR=\"$dacol\">$mo $ye.</FONT>"; }
    if ($pp) { $pp = "p. $pp, "; if ($pp =~ "-") { $pp = "p$pp"; } }
    if ($se) { $se = "[$se], "; }
    if ($pu) { $pu = "$pu, "; }
    if ($bt) { $lbt = "{\\bf In:} {\\it $bt,} ";
	       $bt = "<FONT COLOR=\"$jocol\"><B>In: </B><I>$bt,</I></FONT> "; }
    if ($ed) { $led = " ($ed {\\bf Ed.}), ";
	       $ed = " ($ed <B>Ed.</B>), "; }
    if ($ad && $pu) { $ad = "$ad:$pu"; $pu = ""; } else { $ad = ""; }
    if ($or) { $or = "$or, "; }
    if ($in) { $lin = " {\\bf [$in]}";
	       $in = " <B>[$in]</B>"; }

    ##### Build html representation of entry:
    $ref = "$au$ti$jo$bt$ed$vo$nu$or$pp$pu$ad$dat$in";
    $lref = "\\item $au$lti$ljo$lbt$led$lvo$nu$or$pp$pu$ad$ldat$lin\n\n";
    $lref =~ s/\. /.\\ /g;    # avoid double spaces after periods.

    ##### let's do the latex output first:
    if ($type eq "article" || $type eq "incollection") {
	$ltx1 .= $lref;
    } elsif ($type eq "inproceedings") {
	if ($pp =~ "-" || $lbt =~ "ISMRM" || $lbt =~ "IEEE" || $lbt =~ "SPIE")
	{ $ltx2 .= $lref; }
	else
	{ $ltx3 .= $lref; }
    } else { $ltx4 .= $lref; }

    ##### stop here if we have no entry
    next unless $ref;

    ##### if we have an HTML version, add it:
    if ($entry->exists('url') && length($entry->get('url')) > 3) {
	$txt = icon($ICONhtml, $entry->get('url')); $nhtm ++;
    } else {
	$txt = icon($ICONblank);
    }

    ##### if we have a PS version, add it:
    #if ($entry->exists('psurl') && length($entry->get('psurl')) > 3) {
    #	$txt .= icon($ICONps, $entry->get('psurl')); $nps ++;
    #} else {
    #	$txt .= icon($ICONblank);
    #}

    ##### if we have a PDF version, add it:
    if ($entry->exists('file') && length($entry->get('file')) > 3) {
	$txt .= icon($ICONpdf, $entry->get('file')); $npdf ++;
    } else {
	$txt .= icon($ICONblank);
    }

    ##### create BibTeX entry and add link to it:
    $bname = "$key.bib"; $txt .= icon($ICONbib, $bname);
    open BRE, ">$destdir/$bname" || die "Cannot write $bname: ";
    print BRE $entry->print_s; close BRE;

    ##### if we have an abstract, add a link and create a page with it:
    $fname = "$key.html"; 
    if ($entry->exists('abstract') && length($entry->get('abstract')) > 3) {
	$txt .= icon($ICONabs, $fname);
	$abs = $entry->get('abstract'); $nabs ++;
	$abs =~ s/\\+//g;  # cleanup bogus \'s that remain...
	$abs = "<P class=\"BIBabs\"><FONT COLOR=\"$xxcol\">".
	    "<B>Abstract: </B></FONT>$abs</P>\n";
    } else {
	$txt .= icon($ICONblank, $fname); $abs = "";
    }
    if ($entry->exists('keywords') && length($entry->get('keywords'))>3) {
	$kk = $entry->get('keywords');
	$kw = "<P class=\"BIBkw\"><FONT COLOR=\"$xxcol\"><B>Keywords:".
	    " </B></FONT>".join("; ", split(/\|/, $kk))."</P>\n";
    } else { $kw = ""; }
    if ($entry->exists('note') && length($entry->get('note')) > 3) {
	$note = "<P class=\"BIBkw\"><FONT COLOR=\"$xxcol\"><B>Note: </B>".
	    "</FONT>".($entry->get('note'))."</P>\n";
    } else { $note = ""; }
    if ($entry->exists('type')) {
	$th = $entry->get('type'); $ttt = "";
	$th =~ s/\s+/ /g; $th =~ s/\s*\|\s*/\|/g;
	@the = split(/\|/, $th);
	foreach $them (@the) {
	    if (length($ttt) > 1) { $ttt .= ", "; }
	    $ttt .= "<A HREF=\"$them.html\">$types{$them}</A>";
	}
	$ttt = "<P class=\"BIBkw\"><FONT COLOR=\"$xxcol\"><B>Themes: </B>".
	    "</FONT>$ttt</P>\n";
    } else { $ttt = ""; }
    out($key, "<P class=\"BIBent\">$txt $ref</P>\n$abs$kw$note$ttt");
    # close this one right away so that we don't have too many open files
    closeout($key);

    ##### concatenate entire citation:
    $txt = "<P class=\"BIBent\">$txt $ref</P>\n"; $nb ++;

    ##### write this entry to corresponding files:
    if ($ye =~ /submi/) { $ye = 'submitted'; }
    elsif ($ye =~ /press/) { $ye = 'in-press'; }
    out($type, $txt);  # put in file for that type category
    out($ye, $txt);    # write in file for that year
    if ($entry->exists('type')) {
	$th = $entry->get('type');
	$th =~ s/\s+/ /g; $th =~ s/\s*\|\s*/\|/g;
	@the = split(/\|/, $th);
	if ($#the < 0) { print STDERR "   ### Warning: empty theme field.\n"; }
	foreach $them (@the) {
	    $themes{$them} = 1;
	    out($them, $txt);   # add this entry to each theme it covers
	}
    } else { print STDERR "   ### Warning: no theme field...\n"; }
    out('all', $txt);  # write this entry to the 'all publis' page
    $entry->set('keywords', ""); # avoid bogus xfer of keywords across entries
}

##### let's write out the latex publication list for CV:
open PU, ">$destdir/publications.tex" || die "Cannot write publications.tex: ";
print PU "$ltxstart$ltx1$ltxend$ltx2$ltxend$ltx3$ltxend$ltx4$ltxend$ltxnote";
close PU;

##### ok, now we can compile the general index:
if ($out{'in-press'}) { push @years, 'in-press'; }
if ($out{'submitted'}) { push @years, 'submitted'; }

out('index', "<P><B>$nb publications</B>, $nabs with abstract, ".
    "$npdf available as PDF, and $nhtm as HTML.</P>\n"); # and $nps as .ps

out('index', '<H1>Publications by Year</H1><P>');
foreach $y (reverse sort @years) {
    out('index', "<A HREF=\"$y.html\">$y</A> &nbsp; &nbsp; &nbsp; &nbsp; ");
}
out('index', '</P><H1>Publications by Type</H1><UL>');
foreach $y (sort @origtypes) {
    next unless $out{$y};
    $tt = $types{$y}; $tt =~ s/\s+/\&nbsp;/g;
    out('index', "<LI><A HREF=\"$y.html\">$tt</A></LI>\n");
}
out('index', '</UL></P><H1>Publications by Theme</H1><UL>');
foreach $y (sort keys %themes) {
    next unless $out{$y};
    out('index', "<LI><A HREF=\"$y.html\">$types{$y}</A></LI>");
}
out('index', "</UL></P><H1>Master BibTeX File</H1><P>The master BibTeX ".
    "file used to create these pages can be found <A HREF=\"$bibmaster\">".
    "here</A>.</P>");
out('index', "<!--#include file=\"topten.html\" -->");

closeouts();

rename("$destdir/index.html", "$destdir/index.shtml");

exit(0);

##############################################################################
##############################################################################
sub openout {  # name
    my $k = $_[0];
    local *FH; open FH, ">$destdir/$k.html" || die "Cannot write $k.html: ";
    if ($verbose) { print STDERR "##### Creating $k.html\n"; }
    $out{$k} = *FH; print FH $HTMLstart;
    if ($k =~ /^\d+$/) {   # this file is a list by year
	push @years, $k;
	print FH "<H1>Publications for Year $k</H1>\n";
    }  else {              # this file is a list by category
	if ($types{$k}) {  # this is a real category
	    print FH "<H1>$types{$k}</H1>\n";
	} else {           # it's an abstract
	    print FH "<H1>Abstract</H1>\n";
	}
    }
    print FH "$HTMLicons\n";
}

##############################################################################
sub out {
    my ($k, $txt) = @_;
    if (! defined($out{$k})) { openout($k); }
    local *F = $out{$k}; print F $txt;
}

##############################################################################
sub closeouts {
    foreach $k (keys %out) {
	local *FH = $out{$k};
	print FH "\n$HTMLend\n";
	if ($verbose) { print STDERR "##### Closing $k.html\n"; }
	close FH;
    }
}

##############################################################################
sub closeout {
    if ($out{$_[0]}) {
	local *FH = $out{$_[0]};
	print FH "\n$HTMLend\n";
	if ($verbose) { print STDERR "##### Closing $_[0].html\n"; }
	close FH; delete $out{$_[0]};
    }
}

##############################################################################
sub icon {
    if ($_[1]) {
	return "<A HREF=\"$_[1]\"><IMG SRC=\"$_[0]\" BORDER=0></IMG></A> ";
    } else {
	return "<IMG SRC=\"$_[0]\" BORDER=0></IMG> ";
    }
}
